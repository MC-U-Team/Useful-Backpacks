buildscript {
    repositories {
        maven { url = "https://files.minecraftforge.net/maven" }
		maven { url = "https://plugins.gradle.org/m2/" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "3.+", changing: true
		classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.1.2"
    }
}

apply from: "https://raw.githubusercontent.com/HyCraftHD/Blank-Forge-Project/master/git.methods"

apply plugin: "net.minecraftforge.gradle"
apply plugin: "eclipse"
apply plugin: "com.matthewprenger.cursegradle"
apply plugin: "maven"

repositories {
    maven { url = "https://repo.u-team.info" }
    maven { url = "https://minecraft.curseforge.com/api/maven"}
}

loadConfig()

def loadConfig() {
	ext.buildProps = file "build.properties"

	buildProps.withReader {
		def prop = new Properties()
		prop.load(it)
		ext.config = new ConfigSlurper().parse prop
	}
}

getBuildNumber()

group = "info.u-team"
archivesBaseName = "${config.mod.filename}-${config.forge.mcversion}"
version = "${config.mod.version}.${config.buildnumber}" + getAppendix();

def getAppendix() {
	if("${config.mod.snapshot}".toBoolean()) {
		return "-SNAPSHOT"
	} else {
		return ""
	}
}

sourceCompatibility = targetCompatibility = "1.8"

minecraft {
    mappings channel: config.forge.mapping_channel, version: config.forge.mapping
    runs {
        client = {
            //properties "forge.logging.markers": "SCAN,REGISTRIES,REGISTRYDUMP"
            //properties "forge.logging.console.level": "debug"
            workingDirectory project.file("run").canonicalPath
            source sourceSets.main
        }
        server = {
            workingDirectory project.file("run").canonicalPath
            source sourceSets.main
        }
    }
}

def normalManifest = {
	attributes(
		"Specification-Title": config.mod.name,
        "Specification-Vendor": "Team UTeam",
        "Specification-Version": "25.0",
		"Built-On": config.forge.mcversion,
		"Implementation-Title": config.mod.name,
    	"Implementation-Version": project.version,
		"Implementation-Vendor": "Team UTeam",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
	)
}

jar {
	classifier = ""

    from "LICENSE"
       
   	manifest normalManifest
}

task deobfJar(type: Jar) {
	classifier = "dev"

    from sourceSets.main.output
    from "LICENSE"
        
    manifest normalManifest
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    
    from sourceSets.main.allJava
    from "LICENSE"
}

task docJar(type: Jar) {
    classifier = "javadoc"
    
    from javadoc
    from "LICENSE"
    
    tasks.withType(Javadoc) {
		options.addStringOption('Xdoclint:none', '-quiet')
		options.addStringOption('encoding', 'UTF-8')
		options.addStringOption('charSet', 'UTF-8')
	}
}

artifacts {
    archives deobfJar
    archives sourcesJar
    archives docJar
    archives jar
}

sourceSets {
	main {
		java {
			srcDirs += "src/api/java"
		}
		resources {
			srcDirs += "src/api/resources"
		}
	}
}

dependencies {
    minecraft "net.minecraftforge:forge:$config.forge.version"
	compile "info.u-team:u_team_core-${config.forge.mcversion}:${config.uteamcore.version}:dev"
	compile "solar-flux-reborn:SolarFluxReborn:1.13.2:40b" //Just tests
}

compileJava {
    options.encoding = "UTF-8"
}

curseforge {
	apiKey = project.getProperty("token")
	
	tasks.curseforge.dependsOn build
	
  	project {
    	id = config.curse.id
    	releaseType = config.curse.type

		addGameVersion config.forge.mcversion
		
		options {
		    forgeGradleIntegration = false
		}
		
		relations {
      		requiredDependency "u-team-core"
    	}

    	mainArtifact(jar) {
      		displayName = "${config.curse.display}-${config.forge.mcversion}-${project.version}"
    	}
    	
    	addArtifact(deobfJar) {
    		displayName = "${config.curse.display}-${config.forge.mcversion}-${project.version}-dev"
    		changelog = "Mappings: " + config.forge.mapping
    	}
  	}
}

uploadArchives {

	tasks.uploadArchives.dependsOn build

	repositories {
		mavenDeployer {
			if (project.hasProperty("mavenpass")) {
				repository(url: "https://repo.u-team.info") {
					authentication(userName: "maven", password: project.getProperty("mavenpass"))
				}
			}
			pom {
				groupId = project.group
				artifactId = project.archivesBaseName
				version = project.version
				project {
					name project.archivesBaseName
					packaging "jar"
					description "Useful-Backpacks"
						url "https://github.com/MC-U-Team/Useful-Backpacks"
					scm {
						url "https://github.com/MC-U-Team/Useful-Backpacks"
						connection "scm:git:git://github.com/MC-U-Team/Useful-Backpacks.git"
						developerConnection "scm:git:git@github.com:MC-U-Team/Useful-Backpacks.git"
					}
					issueManagement {
						system "github"
						url "https://github.com/MC-U-Team/Useful-Backpacks/issues"
					}
					licenses {
						license {
							name "Apache License 2.0"
							url "https://github.com/MC-U-Team/Useful-Backpacks/blob/1.13.2/LICENSE"
							distribution "repo"
						}
					}

					developers {
						developer {
							name "HyCraftHD"
						}
					}
				}
			}
		}
	}
}

task uploadBoth {
	tasks.uploadBoth.dependsOn uploadArchives
	tasks.uploadBoth.dependsOn "curseforge${config.curse.id}"
	tasks.uploadBoth.dependsOn setBuildNumber
	doLast {
		println "Published both"
	}
}
