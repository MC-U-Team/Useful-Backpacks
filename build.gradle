buildscript {
	repositories {
		maven {
			url = "https://maven.minecraftforge.net"
		}
		maven {
			url = "https://repo.u-team.info"
		}
		mavenCentral()
	}
	dependencies {
		classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "4.1.+", changing: true
		classpath group: "info.u-team.curse_gradle_uploader", name: "curse_gradle_uploader", version: "1.4.1"
	}
}

apply from: "https://gradle-files.u-team.info/default-project.gradle"

apply plugin: "net.minecraftforge.gradle"
apply plugin: "eclipse"
apply plugin: "info.u_team.curse_gradle_uploader"
apply plugin: "maven-publish"

group = "info.u-team"
archivesBaseName = generateArchivesBaseName()
version = generateVersion()

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(8)
	}
}

minecraft {
	mappings channel: config.forge.mapping_channel, version: config.forge.mapping
	
	runs {
		client = {
			workingDirectory project.file("run/client").canonicalPath
			property "mixin.env.disableRefMap", "true"
			mods {
				"${config.mod.modid}" {
					source sourceSets.main
				}
			}
		}
		client_first = {
			parents runs.getByName("client")
			workingDirectory project.file("run/client/one").canonicalPath
			args "--username", "HyCraftHD", "--uuid", "d9202ce0-f6c1-4dc1-9341-2d9091764808"
		}
		client_second = {
			parents runs.getByName("client")
			workingDirectory project.file("run/client/second").canonicalPath
			args "--username", "UTeam", "--uuid", "08e0d9d9-e948-40de-b68d-d5101571acf1"
		}
		server = {
			workingDirectory project.file("run/server").canonicalPath
			property "mixin.env.disableRefMap", "true"
			mods {
				"${config.mod.modid}" {
					source sourceSets.main
				}
			}
		}
		data = {
			workingDirectory project.file("run/data").canonicalPath
			property "mixin.env.disableRefMap", "true"
			args "--mod", config.mod.modid, "--all", "--output", "\"" + file("src/generated/resources").toString() + "\"", "--validate", "--existing", "\"" + sourceSets.main.resources.srcDirs[0] + "\""
			mods {
				"${config.mod.modid}" {
					source sourceSets.main
				}
			}
		}
	}
}

jar {
	classifier = ""
	
	from "LICENSE"
	
	exclude(".cache")
	
	manifest normalManifest
}

task deobfJar(type: Jar) {
	classifier = "dev"
	
	from sourceSets.main.output
	from "LICENSE"
	
	exclude(".cache")
	
	manifest normalManifest
}

task sourcesJar(type: Jar) {
	classifier = "sources"
	
	from sourceSets.main.allJava
	from "LICENSE"
}

task docJar(type: Jar) {
	classifier = "javadoc"
	
	from javadoc
	from "LICENSE"
	
	tasks.withType(Javadoc) {
		options.addStringOption('Xdoclint:none', '-quiet')
		options.addStringOption('encoding', 'UTF-8')
		options.addStringOption('charSet', 'UTF-8')
	}
}

artifacts {
	archives jar
	archives deobfJar
	archives sourcesJar
	archives docJar
}

signAllJars()

sourceSets {
	main {
		java {
			srcDirs += "src/data/java"
			srcDirs += "src/api/java"
			srcDirs += "src/integration/java"
		}
		resources {
			srcDirs += "src/generated/resources"
		}
	}
}

dependencies {
	minecraft getForgeDependency()
	compileOnly getUTeamCoreDependency()
	compileOnly fg.deobf(getJeiApiDependency())
	runtimeOnly fg.deobf(getJeiDependency())
	compileOnly fg.deobf(getCuriosApiDependency())
	runtimeOnly fg.deobf(getCuriosDependency())
}

curseforge {
	afterEvaluate {
		tasks."curseforge${config.curse.id}".onlyIf {
			getValue("token") != null
		}
	}
	
	tasks.curseforge.dependsOn build
	
	apiKey = getValueDefault("token")
	
	project {
		id = config.curse.id
		releaseType = config.curse.type
		
		addGameVersion config.forge.mcversion
		addGameVersion "Forge"
		addGameVersion "Java 8"
		addGameVersion "Java 9"
		addGameVersion "Java 10"
		
		relations {
			requiredDependency "u-team-core"
			optionalDependency "jei"
			optionalDependency "curios"
		}
		
		mainArtifact(jar) {
			displayName = "${config.curse.display}-${config.forge.mcversion}-${project.version}"
			changelogType = "html"
			changelog = "<a href=\"" + generateChangelogUrl() + "\">Changelog of ${project.version}</a>"
		}
		
		addArtifact(deobfJar) {
			displayName = "${config.curse.display}-${config.forge.mcversion}-${project.version}-dev"
			changelogType = "text"
			changelog = "ONLY for development environment!\nMappings: ${config.forge.mapping_channel}_${config.forge.mapping}"
		}
	}
}

tasks.withType(PublishToMavenRepository) {
	onlyIf {
		getValue("mavenpass") != null
	}
}

publishing {
	publish.dependsOn build
	
	repositories {
		maven {
			url "https://repo.u-team.info"
			credentials {
				username = "maven"
				password = getValue("mavenpass")
			}
		}
	}
	publications {
		mavenJava(MavenPublication) {
			artifact jar
			artifact deobfJar
			artifact sourcesJar
			artifact docJar
			
			groupId = project.group
			artifactId = project.archivesBaseName
			version = project.version
			
			pom {
				name = project.archivesBaseName
				description = "${config.github.name}"
				url = "https://github.com/MC-U-Team/${config.github.name}"
				scm {
					url = "https://github.com/MC-U-Team/${config.github.name}"
					connection = "scm:git:git://github.com/MC-U-Team/${config.github.name}.git"
					developerConnection = "scm:git:git@github.com:MC-U-Team/${config.github.name}.git"
				}
				
				issueManagement {
					system = "github"
					url = "https://github.com/MC-U-Team/${config.github.name}/issues"
				}
				licenses {
					license {
						name = "Apache License 2.0"
						url = "https://github.com/MC-U-Team/${config.github.name}/blob/${config.github.branch}/LICENSE"
						distribution = "repo"
					}
				}
				developers {
					developer {
						name = "HyCraftHD"
					}
				}
			}
		}
	}
}

task uploadBoth {
	tasks.uploadBoth.dependsOn build
	tasks.uploadBoth.dependsOn publish
	tasks.uploadBoth.dependsOn "curseforge${config.curse.id}"
	tasks.uploadBoth.dependsOn setBuildNumber
	tasks.uploadBoth.dependsOn tagRelease
	doLast {
		println "Published both"
	}
}

gradle.taskGraph.whenReady {
	if (gradle.taskGraph.hasTask(":curseforge${config.curse.id}") || gradle.taskGraph.hasTask(":publish")) {
		validateBuild()
	}
}